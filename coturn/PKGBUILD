# Maintainer: Vladimir Tsanev <tsachev@gmail.com>

pkgname=coturn
_portname=turnserver
pkgver=4.0.0.1
pkgrel=1
pkgdesc="Free open source implementation of TURN and STUN Server"
arch=('i686' 'x86_64')
url="http://code.google.com/p/coturn/"
license=('custom:New BSD')
depends=('libevent' 'postgresql-libs' 'libmysqlclient' 'hiredis')
conflicts=('rfc5766-turn-server')
provides=('rfc5766-turn-server')
install="$_portname.install"
backup=("etc/turnserver.conf" "etc/turnuserdb.conf")
source=(http://$_portname.open-sys.org/downloads/v$pkgver/$_portname-$pkgver.tar.gz $_portname.service $_portname.tmpfiles.d)
md5sums=('d4cd4b6544a29a8b0c9cf3d6dd2db156'
         'bf568b614a17ee439e831b8f8aa7236a'
         'aa7bf422a9dfba7febb56dc172feb1cf')
sha1sums=('71122a7e742ae6a2ee3ca506eb82a8b491338f5b'
          '0c5b348e793bd52ce0ee38d420b26c9b2a2e2ca5'
          '445e9982549d7ed018bc1fb6176a730313ae3d26')

build() {
  cd "$srcdir/$_portname-$pkgver"

  ./configure --prefix=/usr --manprefix=/usr/share --examplesdir="/usr/share/$_portname/examples" --disable-rpath

  make
}

check() {
  cd "$srcdir/$_portname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$_portname-$pkgver"
  make DESTDIR="$pkgdir" install
  
  install -D "$pkgdir/usr/share/$_portname/examples/etc/turnserver.conf" "$pkgdir/etc/turnserver.conf"
  install -D "$pkgdir/usr/share/$_portname/examples/etc/turnuserdb.conf" "$pkgdir/etc/turnuserdb.conf"
  rm -r "$pkgdir/usr/etc"

  chmod 644 "$pkgdir/usr/lib/libturnclient.a"

  install -Dm 644 "../$_portname.service" "$pkgdir/usr/lib/systemd/system/$_portname.service"

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  sed \
          -e '/^#log-file=\/var\/tmp\/turn.log$/c log-file=\/var\/log\/turnserver\/turn.log' \
          -i "$pkgdir/etc/turnserver.conf"
  sed \
          -e '/^#pidfile="\/var\/run\/turnserver.pid"$/c pidfile="\/var\/run\/turnserver\/turnserver.pid"' \
          -i "$pkgdir/etc/turnserver.conf"

  install -Dm644 "$srcdir/$_portname.tmpfiles.d" "$pkgdir/usr/lib/tmpfiles.d/$_portname.conf"
}
