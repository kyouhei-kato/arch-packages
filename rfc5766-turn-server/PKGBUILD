# Maintainer: Vladimir Tsanev <tsachev@gmail.com>

pkgname=rfc5766-turn-server
_portname=turnserver
pkgver=1.8.7.0
pkgrel=2
pkgdesc="STUN and TURN Relay Server for VoIP and WebRTC"
arch=('i686' 'x86_64')
url="http://code.google.com/p/rfc5766-turn-server/"
license=('custom:New BSD')
depends=('libevent' 'postgresql-libs' 'libmysqlclient' 'hiredis')
install='install'
backup=("etc/turnserver.conf" "etc/turnuserdb.conf")
changelog='ChangeLog'
source=(https://${pkgname}.googlecode.com/files/${_portname}-$pkgver.tar.gz $_portname.service)
md5sums=('6a4104999377d3ecaa5410377b94e9f9' 'bf568b614a17ee439e831b8f8aa7236a')
sha1sums=('426feafbcc9c11e41f324e9f06a159e9b8fa29df' '0c5b348e793bd52ce0ee38d420b26c9b2a2e2ca5')

build() {
  cd "${srcdir}/$_portname-${pkgver}"
  
  ./configure --prefix=/usr --manprefix=/usr/share --examplesdir="/usr/share/$_portname/examples"

  make
}

check() {
  cd "${srcdir}/$_portname-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/$_portname-${pkgver}"
  make DESTDIR="$pkgdir/" install
  
  install -D "$pkgdir/usr/share/$_portname/examples/etc/turnserver.conf" "$pkgdir/etc/turnserver.conf"
  install -D "$pkgdir/usr/share/$_portname/examples/etc/turnuserdb.conf" "$pkgdir/etc/turnuserdb.conf"
  rm -r "$pkgdir/usr/etc"

  chmod 644 "$pkgdir/usr/lib/libturnclient.a"

  install -dm 755 "$pkgdir/var/log/$_portname"
  install -Dm 644 "../$_portname.service" "$pkgdir/usr/lib/systemd/system/$_portname.service"

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/${_portname}/LICENSE"

  sed \
          -e '/^#log-file=\/var\/tmp\/turn.log$/c log-file=\/var\/log\/turnserver\/turn.log' \
          -i "${pkgdir}"/etc/turnserver.conf
}
