# Maintainer: Vladimir Tsanev <tsachev@gmail.com>

pkgname=rfc5766-turn-server
_portname=turnserver
pkgver=1.8.6.0
pkgrel=1
epoch=
pkgdesc="Free open source implementation of TURN and STUN Server"
arch=('i686' 'x86_64')
url="http://code.google.com/p/rfc5766-turn-server/"
license=('custom:BSD New')
makedepends=('gcc>=3.4.4' 'make')
depends=('libevent')
#no redis headers in redi package
optdepends=('postgresql-client: to use postgresql' 'libmysqlclient')
backup=("etc/turnserver.conf" "etc/turnuserdb.conf")
changelog=ChangeLog
source=(https://${pkgname}.googlecode.com/files/${_portname}-$pkgver.tar.gz)
sha1sums=('155990b95d399542db1bb497f1746c1b9bb290d2')

build() {
  cd "${srcdir}/$_portname-${pkgver}"
  
  PREFIX=/usr \
  MANPREFIX=/usr/share \
  EXAMPLESDIR="/usr/share/$pkgname/examples" \
  DOCSDIR="/usr/share/doc/$pkgname" \
  LIBDIR=/usr/lib \
  SCHEMADIR="/usr/share/$pkgname" \
  TURNINCLUDEDIR="/usr/include/turn"
  ./configure

  make
}

check() {
  cd "${srcdir}/$_portname-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/$_portname-${pkgver}"
  make \
    PREFIX="$pkgdir/usr" \
    MANPREFIX="$pkgdir/usr/share" \
    EXAMPLESDIR="$pkgdir/usr/share/$pkgname/examples" \
    DOCSDIR="$pkgdir/usr/share/doc/$pkgname" \
    LIBDIR="$pkgdir/usr/lib" \
    SCHEMADIR="$pkgdir/usr/share/$pkgname" \
    TURNINCLUDEDIR="$pkgdir/usr/include/turn" \
    install
  
  
  install -D "$pkgdir/usr/share/$pkgname/examples/etc/turnserver.conf" "$pkgdir/etc/turnserver.conf"
  install -D "$pkgdir/usr/share/$pkgname/examples/etc/turnuserdb.conf" "$pkgdir/etc/turnuserdb.conf"
  rm -r "$pkgdir/usr/etc"

  chmod 644 "$pkgdir/usr/lib/libturnclient.a"

  install -Dm 644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
