# Maintainer: Vladimir Tsanev <tsachev@gmail.com>

pkgname=rfc5766-turn-server
_portname=turnserver
pkgver=3.1.2.2
pkgrel=1
pkgdesc="STUN and TURN Relay Server for VoIP and WebRTC"
arch=('i686' 'x86_64')
url="http://code.google.com/p/rfc5766-turn-server/"
license=('custom:New BSD')
depends=('libevent' 'postgresql-libs' 'libmysqlclient' 'hiredis')
install='install'
backup=("etc/turnserver.conf" "etc/turnuserdb.conf")
changelog='ChangeLog'
source=(https://$pkgname.googlecode.com/files/$_portname-$pkgver.tar.gz $_portname.service)
md5sums=('7a0bb4e205a6400f94cc38da545fcf43'
         'bf568b614a17ee439e831b8f8aa7236a')
sha1sums=('57dcf3eb13d588c1f9607b5cac93b944f155d7d4'
          '0c5b348e793bd52ce0ee38d420b26c9b2a2e2ca5')

build() {
  cd "$srcdir/$_portname-$pkgver"
  
  ./configure --prefix=/usr --manprefix=/usr/share --examplesdir="/usr/share/$_portname/examples" --disable-rpath

  make
}

check() {
  cd "$srcdir/$_portname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$_portname-$pkgver"
  make DESTDIR="$pkgdir" install
  
  install -D "$pkgdir/usr/share/$_portname/examples/etc/turnserver.conf" "$pkgdir/etc/turnserver.conf"
  install -D "$pkgdir/usr/share/$_portname/examples/etc/turnuserdb.conf" "$pkgdir/etc/turnuserdb.conf"
  rm -r "$pkgdir/usr/etc"

  chmod 644 "$pkgdir/usr/lib/libturnclient.a"

  install -dm 755 "$pkgdir/var/log/$_portname"
  install -Dm 644 "../$_portname.service" "$pkgdir/usr/lib/systemd/system/$_portname.service"

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  sed \
          -e '/^#log-file=\/var\/tmp\/turn.log$/c log-file=\/var\/log\/turnserver\/turn.log' \
          -i "$pkgdir"/etc/turnserver.conf
}
