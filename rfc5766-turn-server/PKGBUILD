# Maintainer: Vladimir Tsanev <tsachev@gmail.com>

pkgname=rfc5766-turn-server
_portname=turnserver
pkgver=1.8.6.1
pkgrel=3
pkgdesc="STUN and TURN Relay Server for VoIP and WebRTC"
arch=('i686' 'x86_64')
url="http://code.google.com/p/rfc5766-turn-server/"
license=('custom:New BSD')
depends=('libevent' 'postgresql-libs' 'libmysqlclient' 'hiredis')
install='install'
backup=("etc/turnserver.conf" "etc/turnuserdb.conf")
changelog='ChangeLog'
source=(https://${pkgname}.googlecode.com/files/${_portname}-$pkgver.tar.gz $_portname.service)
md5sums=('719e68c556ccab87009af56e3010c2be' 'bf568b614a17ee439e831b8f8aa7236a')
sha1sums=('e6ade2686178a530b74af67f4ef75b729db60da1' '0c5b348e793bd52ce0ee38d420b26c9b2a2e2ca5')

build() {
  cd "${srcdir}/$_portname-${pkgver}"
  
  PREFIX=/usr \
  MANPREFIX=/usr/share \
  EXAMPLESDIR="/usr/share/$pkgname/examples" \
  DOCSDIR="/usr/share/doc/$pkgname" \
  LIBDIR=/usr/lib \
  SCHEMADIR="/usr/share/$pkgname" \
  TURNINCLUDEDIR="/usr/include/turn"
  ./configure

  make
}

check() {
  cd "${srcdir}/$_portname-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/$_portname-${pkgver}"
  make \
    PREFIX="$pkgdir/usr" \
    MANPREFIX="$pkgdir/usr/share" \
    EXAMPLESDIR="$pkgdir/usr/share/$pkgname/examples" \
    DOCSDIR="$pkgdir/usr/share/doc/$pkgname" \
    LIBDIR="$pkgdir/usr/lib" \
    SCHEMADIR="$pkgdir/usr/share/$pkgname" \
    TURNINCLUDEDIR="$pkgdir/usr/include/turn" \
    install
  
  
  install -D "$pkgdir/usr/share/$pkgname/examples/etc/turnserver.conf" "$pkgdir/etc/turnserver.conf"
  install -D "$pkgdir/usr/share/$pkgname/examples/etc/turnuserdb.conf" "$pkgdir/etc/turnuserdb.conf"
  rm -r "$pkgdir/usr/etc"

  chmod 644 "$pkgdir/usr/lib/libturnclient.a"

  install -dm 755 "$pkgdir/var/log/$_portname"
  install -Dm 644 "../$_portname.service" "$pkgdir/usr/lib/systemd/system/$_portname.service"

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"

  sed \
          -e '/^#log-file=\/var\/tmp\/turn.log$/c log-file=\/var\/log\/turnserver\/turn.log' \
          -i "${pkgdir}"/etc/turnserver.conf
}
